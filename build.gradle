buildscript {
    repositories {
        mavenLocal()
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://dist.creeper.host/Sponge/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'idea'
    id 'maven-publish'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

/**
 * Version in dev is set to 'dev'
 * Any builds produced by the github actions build script will have a version matching their tag.
 * Version numbers are [Major - MC Version].[Minor - Feature Version].[Patch]-[Full MC Version]
 */
def envVersion = System.getenv("VERSION")
def useOfficial = (System.getenv("JITPACK") != null || System.getenv("OFFICIAL_MAPPINGS") != null);
version = envVersion == null ? "dev" : envVersion
group = 'com.github.alcatrazEscapee' // group name set for jitpack
archivesBaseName = 'chocolate'
println 'Chocolate build.gradle with version \'' + version + '\' and use official = \'' + useOfficial + '\''

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

repositories {
    mavenLocal()
    maven { url 'https://jitpack.io' } // Used by mcjunitlib
}

minecraft {
    // Mappings used are generated by Mappificator (https://github.com/alcatrazEscapee/mappificator)
    // See the project README page for how to generate the mapping set.
    // Otherwise, we use official mappings in a build script version
    if (useOfficial) {
        mappings channel: 'official', version: mc_version
    } else {
        mappings channel: 'snapshot', version: mappings_version
    }
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Run configurations
    runs {
        client {
            workingDirectory project.file('run')
            arg '-mixin.config=' + mod_id + '.mixins.json'
            property 'forge.logging.console.level', 'debug'
            mods {
                chocolate {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            arg '-mixin.config=' + mod_id + '.mixins.json'
            arg '--nogui'
            property 'forge.logging.console.level', 'debug'
            mods {
                chocolate {
                    source sourceSets.main
                }
            }
        }

        serverTest {
            parent server // This run config inherits settings from the server config
            workingDirectory project.file('run')
            main 'com.alcatrazescapee.mcjunitlib.DedicatedTestServerLauncher' // The main class which launches a customized server which then runs JUnit tests
            ideaModule "${project.name}.test" // Tell IDEA to use the classpath of the test module
            property 'forge.logging.console.level', 'unittest' // This logging level prevents any other server information messages and leaves only the unit test output
            environment 'MOD_CLASSES', String.join(File.pathSeparator,
                    "${mod_id}%%${sourceSets.main.output.resourcesDir}",
                    "${mod_id}%%${sourceSets.main.output.classesDir}",
                    "${mod_id}%%${sourceSets.test.output.resourcesDir}",
                    "${mod_id}%%${sourceSets.test.output.classesDir}",
            ) // Forge will ignore all test sources unless we explicitly tell it to include them as mod sources
            environment 'target', 'fmltestserver' // This is a custom service used to launch with ModLauncher's transforming class loader
            mods {
                chocolate {
                    sources sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    testImplementation fg.deobf("com.github.alcatrazEscapee:mcjunitlib:${mcjunitlib_version}-${mc_version}") {
        transitive = false
    }
}

// Puts the mod version into the jar for mods.toml to read from
jar {
    manifest {
        attributes ([
                "Implementation-Version": "${version}",
                "MixinConfigs": "${mod_id}.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
}

curseforge {
    def envApiKey = System.getenv('CURSEFORGE_API_KEY')
    if (envApiKey == null) {
        println 'No Curseforge API Key'
        envApiKey = 'kaput'
        return
    }
    project {
        apiKey = envApiKey
        id = '416306'
        changelog = "[Release Notes ${version}](https://github.com/alcatrazEscapee/Chocolate/releases/tag/${version})"
        changelogType = 'markdown'
        releaseType = 'release'
        addGameVersion "${mc_version}"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = 'com.github.alcatrazEscapee'
            artifactId = "${mod_id}"
            version = "${version}"

            // Adds the sources as an artifact.
            artifact sourcesJar {
                classifier 'sources'
            }
        }
    }
    repositories {
        mavenLocal()
    }
}